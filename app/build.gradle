apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.0'

    defaultConfig {
        applicationId 'net.rafaeltoledo.code'
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName '1.0.0'

        testInstrumentationRunner 'net.rafaeltoledo.code.TestRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures.viewBinding true

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        final sharedTestDir = 'src/sharedTest/java'
        final sharedResourcesDir = 'src/sharedTest/json'
        test.java.srcDirs += sharedTestDir
        test.resources.srcDir(sharedResourcesDir)
        androidTest.java.srcDirs += sharedTestDir
        androidTest.assets.srcDirs += sharedResourcesDir
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests.includeAndroidResources true
    }
}

kapt {
    correctErrorTypes true
}

hilt {
    enableTransformForLocalTests = true
}

def bothTestImplementation(String dependency) {
    project.dependencies.add('testImplementation', dependency)
    project.dependencies.add('androidTestImplementation', dependency)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "androidx.core:core-ktx:$versions.core"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.activity:activity-ktx:$versions.activity"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "android.arch.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "android.arch.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$versions.hiltx"
    kapt "androidx.hilt:hilt-compiler:$versions.hiltx"

    implementation "com.google.android.material:material:$versions.material"

    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hilt"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    bothTestImplementation 'androidx.test:core-ktx:1.3.0-rc01'
    bothTestImplementation 'androidx.test.ext:junit-ktx:1.1.2-rc01'
    bothTestImplementation 'androidx.test.ext:truth:1.3.0-rc01'
    bothTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    bothTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc01'
    bothTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0-rc01'
    bothTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0-rc01'
    bothTestImplementation 'androidx.test:runner:1.3.0-rc01'
    bothTestImplementation 'androidx.test:rules:1.3.0-rc01'
    bothTestImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
    bothTestImplementation 'com.google.truth:truth:1.0.1'
    bothTestImplementation 'org.robolectric:annotations:4.3.1'
    bothTestImplementation "com.google.dagger:hilt-android-testing:$versions.hilt"

    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7'
    testAnnotationProcessor "com.google.dagger:hilt-android-compiler:$versions.hilt"

    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation 'io.mockk:mockk-android:1.10.0'
    androidTestUtil 'androidx.test:orchestrator:1.3.0-rc01'
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$versions.hilt"
}

def classes = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug")
def sources = files("$projectDir/src/main/java")
def report = "$buildDir/reports/jacoco/report.xml"

task createCoverageReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    sourceDirectories.setFrom(sources)
    classDirectories.setFrom(files(classes))
    executionData.setFrom(fileTree(dir: buildDir, includes: ['jacoco/testDebugUnitTest.exec']))

    reports {
        xml.enabled = true
        xml.destination file(report)
        html.enabled = true
    }
}